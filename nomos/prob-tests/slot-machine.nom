type slot = /\ <{1.}| p+{won{0.2} : potlist * \/ slot,
                         lost{0.8} : \/ slot}

type potlist = +{cons : |{*}> potlist, nil : 1}

proc asset nil : . |{*}- ($p : potlist) =
{
  $p.nil ;
  close $p
}

proc asset cons : ($x : potlist) |{*}- ($p : potlist) =
{
  $p.cons ;
  pay $p {*} ;
  $p <- $x
}

proc contract machine : ($p : potlist) |{*}- (#sl : slot) =
{
  $ll <- accept #sl ;
  get $ll {*} ;
  flip {0.2} (
    HH => $ll..won ;
          $pnew <- cons <- $p ;
          send $ll $pnew ;
          #sl <- detach $ll ;
          $pnil <- nil <- ;
          #sl <- machine <- $pnil
  | TT => $ll..lost ;
          $pnew <- cons <- $p ;
          #sl <- detach $ll ;
          #sl <- machine <- $pnew
  )
}